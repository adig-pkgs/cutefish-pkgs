# Maintainer: noone <>
pkgname=cutefish-core-git
pkgver=r151.d14552c
pkgrel=1
pkgdesc="Cutefish Core"
arch=('x86_64')
url="https://github.com/cutefishos/core.git"
license=('GPL3')
groups=('cutefish-git')
depends=('qt5-declarative' 'polkit-qt5' 'kwindowsystem' 'libpulse' 'libxtst')
makedepends=('git' 'qt5-quickcontrols2' 'cmake' 'extra-cmake-modules' 'fishui' 'ninja')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}"
	'remove_translation.patch')
md5sums=('SKIP'
	'b7113ff5872995d523b4014cd7fd3c17')

prepare() {
        cd "$srcdir/${pkgname%-git}"

        # Apply patches
        git apply "$srcdir"/*.patch
}

pkgver() {
        cd "$srcdir/${pkgname%-git}"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
        mkdir -p build && cd build
        rm -f CMakeCache.txt
	cmake \
		-G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		..
	cmake --build .
}

package() {
	cd "$srcdir/${pkgname%-git}"
        cd build
	DESTDIR="$pkgdir/" cmake --install .
}

