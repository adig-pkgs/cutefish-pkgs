# Maintainer: noone <>
pkgname=cutefish-statusbar-git
pkgver=r137.65f601e
pkgrel=1
pkgdesc="Cutefish Statusbar"
arch=('x86_64')
url="https://github.com/cutefishos/statusbar.git"
license=('GPL3')
groups=('cutefish-git')
depends=('qt5-declarative' 'qt5-svg' 'qt5-quickcontrols2' 'fishui' 'kwindowsystem')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'gcc' 'ninja')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}"
	'remove_translation.patch')
md5sums=('SKIP'
	'db2ffb0cfba2cf95bf91ff03005f586f')

prepare() {
        cd "$srcdir/${pkgname%-git}"

        # Apply patches
        git apply "$srcdir"/*.patch
}

pkgver() {
        cd "$srcdir/${pkgname%-git}"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
        mkdir -p build && cd build
        rm -f CMakeCache.txt
	cmake \
		-G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		..
	cmake --build .
}

package() {
	cd "$srcdir/${pkgname%-git}"
        cd build
	DESTDIR="$pkgdir/" cmake --install .
}

