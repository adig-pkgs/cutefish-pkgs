# Maintainer: noone <>
pkgname=cutefish-terminal-git
pkgver=r4.3fbb988
pkgrel=1
pkgdesc="Cutefish Terminal"
arch=('x86_64')
url="https://github.com/cutefishos/terminal.git"
license=('GPL3')
groups=('cutefish-git')
depends=('qt5-declarative')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'gcc' 'ninja' 'qt5-quickcontrols2' 'fontconfig')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}"
	'remove_translation.patch')
md5sums=('SKIP'
	'ece68a58f2e11510e8f9a44241282f11')

prepare() {
        cd "$srcdir/${pkgname%-git}"

        # Apply patches
        git apply "$srcdir"/*.patch
}

pkgver() {
        cd "$srcdir/${pkgname%-git}"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
        mkdir -p build && cd build
        rm -f CMakeCache.txt
	cmake \
		-G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		..
	cmake --build .
}

package() {
	cd "$srcdir/${pkgname%-git}"
        cd build
	DESTDIR="$pkgdir/" cmake --install .
}

